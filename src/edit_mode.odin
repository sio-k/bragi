package main

import "core:encoding/uuid"
import "core:log"
import "core:slice"
import "core:strings"

// edit mode is when we're editing a file
edit_mode_keyboard_event_handler :: proc(event: Event_Keyboard, cmd: Command) -> bool {
    pane := active_pane
    buffer := pane.buffer

    if event.is_text_input {
        length_of_inserted_text := insert_at_points(pane, event.text)
        return length_of_inserted_text > 0
    }

    // handle the generic ones first
    #partial switch event.key_code {
        case .K_BACKSPACE: {
            t: Translation = .left

            if .Ctrl in event.modifiers || .Alt in event.modifiers {
                t = .prev_word
            }

            remove_to(pane, t)
            return true
        }
        case .K_ENTER: {
            insert_newlines_and_indent(pane)
            return true
        }
        case .K_TAB: {
            maybe_indent_or_go_to_tab_stop(pane)
            return true
        }
        case .K_DELETE: {
            t: Translation = .right

            if .Ctrl in event.modifiers || .Alt in event.modifiers {
                t = .next_word
            }

            remove_to(pane, t)
            return true
        }
    }

    switch cmd {
    case .noop:      return false // not handled, it should report for now
    case .modifier:  // handled globally
    case .quit_mode: // handled globally

    case .increase_font_size:
        current_index, _ := slice.binary_search(font_sizes, pane.local_font_size)
        if current_index + 1 < len(font_sizes) {
            pane.local_font_size = font_sizes[current_index + 1]
            update_all_pane_textures()
        }
        return true
    case .decrease_font_size:
        current_index, _ := slice.binary_search(font_sizes, pane.local_font_size)
        if current_index > 0 {
            pane.local_font_size = font_sizes[current_index - 1]
            update_all_pane_textures()
        }
        return true
    case .reset_font_size:
        default_font_size := i32(settings.editor_font_size)
        if pane.local_font_size != default_font_size {
            pane.local_font_size = default_font_size
            update_all_pane_textures()
        }
        return true

    case .toggle_selection_mode:
        editor_toggle_selection(pane)
        return true

    case .prev_cursor:
        if pane.cursor_selecting {
            pane.cursor_selecting = false
            for &cursor in pane.cursors do cursor.sel = cursor.pos
        }

        current_cursor_index := -1

        for &cursor, index in pane.cursors {
            if cursor.active {
                current_cursor_index = index
                cursor.active = false
            }
        }

        current_cursor_index -= 1
        if current_cursor_index < 0 do current_cursor_index = len(pane.cursors) - 1
        pane.cursors[current_cursor_index].active = true
        return true
    case .next_cursor:
        if pane.cursor_selecting {
            pane.cursor_selecting = false
            for &cursor in pane.cursors do cursor.sel = cursor.pos
        }

        current_cursor_index := -1

        for &cursor, index in pane.cursors {
            if cursor.active {
                current_cursor_index = index
                cursor.active = false
            }
        }

        current_cursor_index += 1
        if current_cursor_index > len(pane.cursors) - 1 do current_cursor_index = 0
        pane.cursors[current_cursor_index].active = true
        return true
    case .all_cursors:
        for &cursor in pane.cursors do cursor.active = true
        return true
    case .recenter_cursor:
        maybe_recenter_cursor(pane, true)
        return true

    case .clone_cursor_start:
        clone_to(pane, .start)
        return true
    case .clone_cursor_end:
        clone_to(pane, .end)
        return true
    case .clone_cursor_left:
        clone_to(pane, .left)
        return true
    case .clone_cursor_right:
        clone_to(pane, .right)
        return true
    case .clone_cursor_down:
        clone_to(pane, .down)
        return true
    case .clone_cursor_up:
        clone_to(pane, .up)
        return true
    case .clone_cursor_prev_word:
        clone_to(pane, .prev_word)
        return true
    case .clone_cursor_next_word:
        clone_to(pane, .next_word)
        return true
    case .clone_cursor_prev_paragraph:
        clone_to(pane, .prev_paragraph)
        return true
    case .clone_cursor_next_paragraph:
        clone_to(pane, .next_paragraph)
        return true
    case .clone_cursor_beginning_of_line:
        clone_to(pane, .beginning_of_line)
        return true
    case .clone_cursor_end_of_line:
        clone_to(pane, .end_of_line)
        return true

    case .move_start:
        move_to(pane, .start)
        return true
    case .move_end:
        move_to(pane, .end)
        return true
    case .move_left:
        move_to(pane, .left)
        return true
    case .move_right:
        move_to(pane, .right)
        return true
    case .move_down:
        move_to(pane, .down)
        return true
    case .move_up:
        move_to(pane, .up)
        return true
    case .move_prev_word:
        move_to(pane, .prev_word)
        return true
    case .move_next_word:
        move_to(pane, .next_word)
        return true
    case .move_prev_paragraph:
        move_to(pane, .prev_paragraph)
        return true
    case .move_next_paragraph:
        move_to(pane, .next_paragraph)
        return true
    case .move_prev_page:
        move_to(pane, .prev_page)
        maybe_recenter_cursor(pane)
        return true
    case .move_next_page:
        move_to(pane, .next_page)
        maybe_recenter_cursor(pane)
        return true
    case .move_beginning_of_line:
        move_to(pane, .beginning_of_line)
        return true
    case .move_end_of_line:
        move_to(pane, .end_of_line)
        return true

    case .select_all:
        clear(&pane.cursors)
        add_cursor(pane, len(pane.contents))
        pane.cursors[0].pos = 0
        return true
    case .select_start:
        select_to(pane, .start)
        return true
    case .select_end:
        select_to(pane, .end)
        return true
    case .select_left:
        select_to(pane, .left)
        return true
    case .select_right:
        select_to(pane, .right)
        return true
    case .select_down:
        select_to(pane, .down)
        return true
    case .select_up:
        select_to(pane, .up)
        return true
    case .select_prev_word:
        select_to(pane, .prev_word)
        return true
    case .select_next_word:
        select_to(pane, .next_word)
        return true
    case .select_prev_paragraph:
        select_to(pane, .prev_paragraph)
        return true
    case .select_next_paragraph:
        select_to(pane, .next_paragraph)
        return true
    case .select_prev_page:
        select_to(pane, .prev_page)
        return true
    case .select_next_page:
        select_to(pane, .next_page)
        return true
    case .select_beginning_of_line:
        select_to(pane, .beginning_of_line)
        return true
    case .select_end_of_line:
        select_to(pane, .end_of_line)
        return true

    case .remove_left:
        remove_to(pane, .left)
    case .remove_right:
        remove_to(pane, .right)

    case .find_buffer:
        widget_open_find_buffer()
        return true
    case .find_file:
        widget_open_find_file()
        return true

    case .close_current_buffer:
        index := buffer_index(buffer)
        ordered_remove(&open_buffers, index)
        buffer_destroy(buffer)
        active_pane.buffer = nil
        if len(open_buffers) == 0 {
            switch_to_buffer(active_pane, buffer_get_or_create_empty())
        } else {
            index = clamp(index, 0, len(open_buffers) - 1)
            switch_to_buffer(active_pane, open_buffers[index])
        }
        return true
    case .save_buffer:
        if buffer.filepath == "" {
            widget_open_save_file_as()
        } else {
            buffer_save(buffer)
        }

        return true

    case .save_buffer_as:
        widget_open_save_file_as()
        return true

    case .search_backward:
    case .search_forward:
        widget_open_search_in_buffer()

    case .close_this_pane:
        if len(open_panes) == 1 do return true
        pane_index_to_close := -1
        for other, index in open_panes {
            if active_pane.uuid == other.uuid {
                pane_index_to_close = index
                break
            }
        }
        new_pane_index := pane_index_to_close < len(open_panes) - 1 ? pane_index_to_close + 1 : 0
        old_pane := active_pane
        active_pane = open_panes[new_pane_index]
        ordered_remove(&open_panes, pane_index_to_close)
        pane_destroy(old_pane)
        update_all_pane_textures()
        return true
    case .close_other_panes:
        if len(open_panes) == 1 do return true
        ids_to_remove := make([dynamic]uuid.Identifier, 0, len(open_panes), context.temp_allocator)
        for other in open_panes {
            if active_pane.uuid != other.uuid do append(&ids_to_remove, other.uuid)
        }
        for len(ids_to_remove) > 0 {
            pane_id := pop(&ids_to_remove)

            for other, index in open_panes {
                if other.uuid == pane_id {
                    unordered_remove(&open_panes, index)
                    pane_destroy(other)
                    break
                }
            }
        }

        update_all_pane_textures()
        return true
    case .new_pane_to_the_right:
        result := pane_create()
        switch_to_buffer(result, buffer)
        active_pane = result
        return true
    case .other_pane:
        if len(open_panes) == 1 do return true
        other_pane_index := -1
        for other, index in open_panes {
            if active_pane.uuid == other.uuid {
                other_pane_index = index
                break
            }
        }
        other_pane_index = other_pane_index < len(open_panes) - 1 ? other_pane_index + 1 : 0
        old_pane := active_pane
        active_pane = open_panes[other_pane_index]
        // repaiting the old pane and the new pane
        flag_pane(old_pane, {.Need_Full_Repaint})
        flag_pane(active_pane, {.Need_Full_Repaint})
        return true

    case .undo:
        copy_cursors(pane, buffer)
        undo_done, cursors, pieces := undo(buffer, &buffer.undo, &buffer.redo)

        if !undo_done {
            log.debug("no more history to undo")
            return true
        }

        delete(pane.cursors)
        delete(buffer.pieces)
        pane.cursors = slice.clone_to_dynamic(cursors)
        buffer.pieces = slice.clone_to_dynamic(pieces)
        editor_toggle_selection(pane, true)
        return true
    case .redo:
        copy_cursors(pane, buffer)
        redo_done, cursors, pieces := undo(buffer, &buffer.redo, &buffer.undo)

        if !redo_done {
            log.debug("no more history to redo")
            return true
        }

        delete(pane.cursors)
        delete(buffer.pieces)
        pane.cursors = slice.clone_to_dynamic(cursors)
        buffer.pieces = slice.clone_to_dynamic(pieces)
        editor_toggle_selection(pane, true)
        return true

    case .cut_region:
    case .cut_line:
        remove_to(pane, .end_of_line)
        return true
    case .copy_region:
    case .copy_line:
    case .paste:
    case .paste_from_history:
    }

    return false
}

clone_to :: proc(pane: ^Pane, t: Translation) {
    if len(pane.cursors) == 1 {
        cloned := clone_cursor(pane, pane.cursors[0])
        move_to(pane, t, cloned)
    } else if !are_all_cursors_active(pane) {
        cursor_to_clone := get_first_active_cursor(pane)
        cloned := clone_cursor(pane, cursor_to_clone^)
        move_to(pane, t, cloned)
        cursor_to_clone.active = false
        cloned.active = true
    } else {
        pane.cursor_selecting = false
        for &cursor in pane.cursors do cursor.sel = cursor.pos

        switch t {
        case .start: unimplemented()
        case .end: unimplemented()
        case .left: unimplemented()
        case .right: unimplemented()
        case .prev_word: unimplemented()
        case .next_word: unimplemented()
        case .prev_paragraph: unimplemented()
        case .next_paragraph: unimplemented()
        case .prev_page: unimplemented()
        case .next_page: unimplemented()
        case .beginning_of_line: unimplemented()
        case .end_of_line: unimplemented()
        case .up:
            cursor_to_clone: Cursor
            lo_pos := len(pane.contents)

            for cursor in pane.cursors {
                lo_pos = min(lo_pos, cursor.pos)
                if lo_pos == cursor.pos do cursor_to_clone = cursor
            }
            cloned := clone_cursor(pane, cursor_to_clone)
            move_to(pane, t, cloned)
        case .down:
            cursor_to_clone: Cursor
            hi_pos := 0

            for cursor in pane.cursors {
                hi_pos = max(hi_pos, cursor.pos)
                if hi_pos == cursor.pos do cursor_to_clone = cursor
            }
            cloned := clone_cursor(pane, cursor_to_clone)
            move_to(pane, t, cloned)
        }
    }
}

move_to :: proc(pane: ^Pane, t: Translation, cursor_to_move: ^Cursor = nil) {
    move_cursor :: #force_inline proc(pane: ^Pane, cursor: ^Cursor, t: Translation) {
        if t == .left && has_selection(cursor^) {
            low, _ := sorted_cursor(cursor^)
            cursor.pos = low
            cursor.sel = low
        } else if t == .right && has_selection(cursor^) {
            _, high := sorted_cursor(cursor^)
            cursor.pos = high
            cursor.sel = high
        } else {
            coords := cursor_offset_to_coords(pane, get_lines_array(pane), cursor.pos)
            last_column := -1 if t != .up && t != .down else max(cursor.last_column, coords.column)
            result, result_column := translate_position(pane, cursor.pos, t, last_column)
            cursor.pos = result
            cursor.sel = result
            cursor.last_column = result_column
        }
    }

    if cursor_to_move != nil {
        move_cursor(pane, cursor_to_move, t)
    } else {
        if pane.cursor_selecting {
            select_to(pane, t)
        } else {
            for &cursor in pane.cursors {
                if !cursor.active do continue
                move_cursor(pane, &cursor, t)
            }
        }
    }

    _maybe_merge_overlapping_cursors(pane)
}

select_to :: proc(pane: ^Pane, t: Translation, cursor_to_select: ^Cursor = nil) {
    if cursor_to_select != nil {
        cursor_to_select.pos, _ = translate_position(pane, cursor_to_select.pos, t)
    } else {
        for &cursor in pane.cursors {
            if !cursor.active do continue
            cursor.pos, _ = translate_position(pane, cursor.pos, t)
        }
    }

    _maybe_merge_overlapping_cursors(pane)
}

remove_to :: proc(pane: ^Pane, t: Translation) -> (total_amount_of_removed_characters: int) {
    profiling_start("removing text")
    copy_cursors(pane, pane.buffer)

    for &cursor in pane.cursors {
        if !cursor.active do continue

        if !has_selection(cursor) {
            cursor.pos, _ = translate_position(pane, cursor.pos, t)
        }
    }

    remove_selections(pane)

    profiling_end()
    return
}

remove_selections :: proc(pane: ^Pane, array: ^[dynamic]Cursor = nil) {
    cursors_array := array == nil ? &pane.cursors : array

    copy_cursors(pane, pane.buffer)

    for &cursor, current_index in cursors_array {
        if !cursor.active do continue

        if has_selection(cursor) {
            low, high := sorted_cursor(cursor)
            offset := high - low

            if low != high {
                remove_at(pane.buffer, low, offset)
                cursor.pos = low
                cursor.sel = low
            }

            for &other, other_index in cursors_array {
                if current_index == other_index do continue

                if other.pos > cursor.pos {
                    other.pos -= offset
                    other.sel -= offset
                }
            }
        }
    }

    pane.cursor_selecting = false
    _maybe_merge_overlapping_cursors(pane)
}

insert_at_points :: proc(pane: ^Pane, text: string) -> (total_length_of_inserted_characters: int) {
    profiling_start("inserting text input")
    copy_cursors(pane, pane.buffer)

    // just check the first token, if it's a closing indentation
    // token, figure out if it was also the first token in the
    // line that is not an indentation character.
    indent_tokens := get_indentation_tokens(pane.buffer, text)
    maybe_should_reindent := len(indent_tokens) > 0 && indent_tokens[0].action == .Close
    reindent_amount := 0

    switch pane.buffer.indent.tab_char {
    case .space: reindent_amount = pane.buffer.indent.tab_size
    case .tab:   reindent_amount = 1
    }

    if maybe_should_reindent {
        for &cursor in pane.cursors {
            if !cursor.active do continue

            buffer_lines := pane.line_starts[:]
            line_index := get_line_index(cursor.pos, buffer_lines)
            line_text := get_line_text_until_offset(pane, line_index, buffer_lines, cursor.pos)
            should_reindent := true

            for r in line_text {
                should_reindent = r == ' ' || r == '\t'
                if !should_reindent do break
            }

            if should_reindent {
                cursor.sel -= reindent_amount
            }
        }
    }

    remove_selections(pane)

    for &cursor, current_index in pane.cursors {
        if !cursor.active do continue

        delta_offset := insert_at(pane.buffer, cursor.pos, text)
        total_length_of_inserted_characters += delta_offset
        cursor.pos += delta_offset
        cursor.sel = cursor.pos

        for &other, other_index in pane.cursors {
            if current_index == other_index do continue

            if other.pos > cursor.pos {
                other.pos += delta_offset
                other.sel += delta_offset
            }
        }
    }

    profiling_end()
    return
}

insert_newlines_and_indent :: proc(pane: ^Pane) -> (total_length_of_inserted_characters: int) {
    profiling_start("inserting newline and indenting")
    copy_cursors(pane, pane.buffer)

    remove_selections(pane)

    // TODO(nawe) should also reindent current line
    for &cursor, current_index in pane.cursors {
        if !cursor.active do continue
        buffer_lines := pane.line_starts[:]
        line_index := get_line_index(cursor.pos, buffer_lines)
        count_by_characters := get_line_indent_count(pane, line_index, buffer_lines)
        line_text := get_line_text(pane, line_index, buffer_lines)
        indent_tokens := get_indentation_tokens(pane.buffer, line_text)
        delta := _calculate_indent_delta(indent_tokens)

        // since we do electric indentation, we should have taken care
        // of the indent delta of this line, but if the user has
        // closed the block after the soft line start, we want to make
        // sure we take into account that amount of delta too.
        if delta < 0 && line_index > 0 {
            indent_chars_prev_line := get_line_indent_count(pane, line_index - 1, buffer_lines)
            if indent_chars_prev_line > count_by_characters do delta += 1
        }

        total_indent_count := count_by_characters
        switch pane.buffer.indent.tab_char {
        case .space: total_indent_count += delta * pane.buffer.indent.tab_size
        case .tab:   total_indent_count += delta
        }
        total_indent_count = max(total_indent_count, 0)

        text_to_insert := strings.builder_make(context.temp_allocator)
        strings.write_string(&text_to_insert, "\n")

        for _ in 0..<total_indent_count {
            switch pane.buffer.indent.tab_char {
            case .space: strings.write_string(&text_to_insert, " ")
            case .tab:   strings.write_string(&text_to_insert, "\t")
            }
        }

        offset := insert_at(pane.buffer, cursor.pos, strings.to_string(text_to_insert))
        cursor.pos += offset
        cursor.sel = cursor.pos
        total_length_of_inserted_characters += offset

        for &other, other_index in pane.cursors {
            if current_index == other_index do continue

            if other.pos > cursor.pos {
                other.pos += offset
                other.sel += offset
            }
        }
    }
    profiling_end()
    return
}

// TODO(nawe) this implementation isn't good, it's just something I
// put together quickly and works reliably only on one cursor.
maybe_indent_or_go_to_tab_stop :: proc(pane: ^Pane) {
    Line_Indent_Info :: struct {
        line_start_offset: int,
        indent_amount:     int,
    }

    line_has_code :: proc(pane: ^Pane, start, end: int) -> bool {
        for r in pane.contents[start:end] do if r != '\t' || r != ' ' do return true
        return false
    }

    copy_cursors(pane, pane.buffer)

    for &cursor in pane.cursors {
        if !cursor.active do continue

        buffer_lines := pane.line_starts[:]
        line_index := get_line_index(cursor.pos, buffer_lines)
        count_of_characters_wanted := 0
        if line_index == 0 do continue

        test_line_index := line_index - 1
        for test_line_index > 0 && count_of_characters_wanted == 0 {
            count_of_characters_wanted = get_line_indent_count(pane, test_line_index, buffer_lines)
            test_line_index -= 1
        }

        if count_of_characters_wanted == 0 do continue

        indent_characters_in_curr_line := get_line_indent_count(pane, line_index, buffer_lines)
        curr_line_start, curr_line_end := get_line_boundaries(line_index, buffer_lines)

        if curr_line_start != curr_line_end && line_has_code(pane, curr_line_start, curr_line_end) {
            prev_line_text := get_line_text(pane, line_index - 1, buffer_lines)
            curr_line_text := get_line_text(pane, line_index, buffer_lines)
            prev_line_indent_tokens := get_indentation_tokens(pane.buffer, prev_line_text)
            curr_line_indent_tokens := get_indentation_tokens(pane.buffer, curr_line_text)
            delta := _calculate_indent_delta(prev_line_indent_tokens)

            // the start of the previous line was a closing token
            if len(prev_line_indent_tokens) > 0 && prev_line_indent_tokens[0].action == .Close {
                delta += 1
            }

            // the start of this line is a closing token
            if len(curr_line_indent_tokens) > 0 && curr_line_indent_tokens[0].action == .Close {
                delta -= 1
            }

            count_of_characters_wanted = _calculate_new_indent(pane.buffer, count_of_characters_wanted, delta)
        }

        if count_of_characters_wanted != indent_characters_in_curr_line {
            offset := count_of_characters_wanted - indent_characters_in_curr_line

            if offset > 0 {
                builder := strings.builder_make(context.temp_allocator)
                for _ in 0..<offset {
                    switch pane.buffer.indent.tab_char {
                    case .space: strings.write_string(&builder, " ")
                    case .tab:   strings.write_string(&builder, "\t")
                    }
                }

                insert_at(pane.buffer, curr_line_start, strings.to_string(builder))
            } else {
                remove_at(pane.buffer, curr_line_start, abs(offset))
            }

            cursor.pos += offset
            cursor.sel += offset
        }

        if cursor.pos - curr_line_start < count_of_characters_wanted {
            cursor.pos = curr_line_start + count_of_characters_wanted
            cursor.sel = cursor.pos
        }
    }
}

maybe_recenter_cursor :: proc(pane: ^Pane, force_recenter := false) {
    cursor := get_first_active_cursor(pane)
    lines := get_lines_array(pane)
    coords := cursor_offset_to_coords(pane, lines, cursor.pos)
    top_edge := pane.y_offset
    bottom_edge := pane.y_offset + pane.visible_rows
    right_edge := pane.visible_columns

    if force_recenter || coords.row < top_edge || coords.row > bottom_edge {
        pane.y_offset = clamp(coords.row - pane.visible_rows/2, 0, len(lines) - pane.visible_rows/2)

        if coords.column < right_edge {
            pane.x_offset = 0
        } else {
            pane.x_offset = coords.column/2
        }
    }
}

editor_toggle_selection :: proc(pane: ^Pane, force_reset := false) {
    if pane.cursor_selecting || force_reset {
        pane.cursor_selecting = false
        for &cursor in pane.cursors {
            cursor.sel = cursor.pos
        }
    } else {
        pane.cursor_selecting = true
        for &cursor in pane.cursors {
            cursor.active = true
        }
    }
}

@(private="file")
_maybe_merge_overlapping_cursors :: proc(pane: ^Pane) {
    if len(pane.cursors) < 2 do return

    for i in 0..<len(pane.cursors) {
        for j in 1..<len(pane.cursors) {
            if i == j do continue
            icursor := pane.cursors[i]
            jcursor := pane.cursors[j]

            if !has_selection(icursor) && !has_selection(jcursor) {
                ipos := icursor.pos
                jpos := jcursor.pos

                if ipos == jpos {
                    log.debugf("merging cursors {} and {}", i + 1, j + 1)
                    pane.cursors[i].active = true
                    ordered_remove(&pane.cursors, j)
                    flag_pane(pane, {.Need_Full_Repaint})
                }
            } else {
                _, ihi := sorted_cursor(icursor)
                jlo, jhi := sorted_cursor(jcursor)

                if ihi >= jlo && ihi < jhi {
                    if icursor.pos > icursor.sel {
                        // going to the right
                        pane.cursors[i].pos = max(icursor.pos, jcursor.pos)
                        pane.cursors[i].sel = min(icursor.sel, jcursor.sel)
                    } else {
                        // going to the left
                        pane.cursors[i].pos = min(icursor.pos, jcursor.pos)
                        pane.cursors[i].sel = max(icursor.sel, jcursor.sel)
                    }

                    log.debugf("merging cursors {} and {}", i + 1, j + 1)
                    pane.cursors[i].last_column = -1
                    pane.cursors[i].active = true
                    ordered_remove(&pane.cursors, j)
                    flag_pane(pane, {.Need_Full_Repaint})
                }
            }
        }
    }
}

@(private="file")
_calculate_new_indent :: #force_inline proc(buffer: ^Buffer, current_indent, delta: int) -> (result: int) {
    result = current_indent
    switch buffer.indent.tab_char {
    case .space: result += delta * buffer.indent.tab_size
    case .tab:   result += delta
    }
    return max(result, 0)
}

@(private="file")
_calculate_indent_delta :: proc(tokens: []Indentation_Token) -> (delta: int) {
    // maybe this should be a little bit more consistent, making sure
    // that the token that opened the next level of indentation is the
    // first one to be used to close it?

    // Emacs doesn't care if you're closing a block with the correct
    // token, so for now we'll follow the same approach.
    for token in tokens {
        switch token.action {
        case .None: // do nothi
        case .Close: delta -= 1
        case .Open:  delta += 1
        }
    }
    return
}
